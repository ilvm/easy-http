apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def getVersionName = { ->
    return "2.1.1"
}

def getArtifactId = { ->
    return "easyhttp"
}

android {

    namespace 'xds.lib.easyhttp'
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 4
        versionName getVersionName()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

tasks.register("androidJavadoc", Javadoc) {
    dependsOn(tasks.named("compileReleaseJavaWithJavac"))
    source = android.sourceSets.main.java.srcDirs
    classpath = files(
            android.bootClasspath,
            configurations.named("releaseCompileClasspath"),
            layout.buildDirectory.dir("intermediates/javac/release/compileReleaseJavaWithJavac/classes")
    )
    options.encoding = "UTF-8"
    options.addStringOption("Xdoclint:none", "-quiet")
    failOnError = false
    exclude "**/BuildConfig.java"
    destinationDir = layout.buildDirectory
            .dir("docs/javadoc")
            .get()
            .asFile
}

tasks.register("javadocJar", Jar) {
    dependsOn(tasks.named("androidJavadoc"))
    archiveClassifier.set('javadoc')
    from(tasks.named("androidJavadoc").map { it.destinationDir })
}

tasks.register("sourcesJar", Jar) {
    archiveClassifier.set('sources')
    from(android.sourceSets.main.java.srcDirs)
}

publishing {

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/ilvm/easy-http"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId = 'xds.lib'
            artifactId = getArtifactId()
            version = getVersionName()
            artifact("$buildDir/outputs/aar/core-release.aar")
            artifact(tasks.named("javadocJar"))
            artifact(tasks.named("sourcesJar"))
        }
    }
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.8.2'
}
